plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
}

val uodaReleaseKeystorePath = System.getenv("UODA_RELEASE_KEYSTORE_PATH")
val uodaReleaseStorePassword = System.getenv("UODA_RELEASE_STORE_PASSWORD")
val uodaReleaseKeyAlias = System.getenv("UODA_RELEASE_KEY_ALIAS")
val uodaReleaseKeyPassword = System.getenv("UODA_RELEASE_KEY_PASSWORD")
    ?.takeIf { it.isNotBlank() }
    ?: uodaReleaseStorePassword
val uodaReleaseSigningConfigured = listOf(
    uodaReleaseKeystorePath,
    uodaReleaseStorePassword,
    uodaReleaseKeyAlias,
    uodaReleaseKeyPassword
).all { !it.isNullOrBlank() }

android {
    namespace = "com.defname.unlimitedondemandautoreply"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.defname.unlimitedondemandautoreply"
        minSdk = 24
        targetSdk = 35
        versionCode = 7
        versionName = "v0.7-alpha"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (uodaReleaseSigningConfigured) {
            create("release") {
                storeFile = file(uodaReleaseKeystorePath!!)
                storePassword = uodaReleaseStorePassword
                keyAlias = uodaReleaseKeyAlias
                keyPassword = uodaReleaseKeyPassword
            }
        }
    }

    buildTypes {
        release {
            if (uodaReleaseSigningConfigured) {
                signingConfig = signingConfigs.getByName("release")
            }
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose = true
    }
}

dependencies {

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.core.splashscreen)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)
    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)
}
